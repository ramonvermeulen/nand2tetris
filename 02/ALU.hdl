// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

/* Tip: I drew this out on paper first, otherwise it is almost impossible */
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=xZeroed);
    Mux16(a=y, b=false, sel=zy, out=yZeroed);
    Not16(in=xZeroed, out=xZeroedFlipped);
    Not16(in=yZeroed, out=yZeroedFlipped);
    Mux16(a=xZeroed, b=xZeroedFlipped, sel=nx, out=xCleaned);
    Mux16(a=yZeroed, b=yZeroedFlipped, sel=ny, out=yCleaned);

    And16(a=xCleaned, b=yCleaned, out=outAnd);
    Add16(a=xCleaned, b=yCleaned, out=outAdd);
    Mux16(a=outAnd, b=outAdd, sel=f, out=outF);

    Not16(in=outF, out=outFFlipped);
    Mux16(
        a=outF,
        b=outFFlipped,
        sel=no,
        out=out, // final output
        // rest is for nr and zo outputs
        out[15]=outFirstBit,
        out[0..7]=firstHalf,
        out[8..15]=secondHalf
    );

    And(a=true, b=outFirstBit, out=ng); // ng output (simple: check first bit e.g. signed vs unsigned)

    // zero out (use 2x Or8Way to check if bot halfs only contains zeros, we didn't make a Or16Way in the course)
    Or8Way(in=firstHalf, out=firstHalfOut);
    Or8Way(in=secondHalf, out=secondHalfOut);

    // and then combine and flip the result
    Or(a=firstHalfOut, b=secondHalfOut, out=zrNotFlipped);
    Not(in=zrNotFlipped, out=zr); // zr output
}
